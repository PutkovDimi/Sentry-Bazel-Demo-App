load("@bazel_gazelle//:def.bzl", "gazelle", "gazelle_binary")
load("@bazel_skylib//rules:build_test.bzl", "build_test")
load("@build_bazel_rules_apple//apple:ios.bzl", "ios_application")
load("@build_bazel_rules_swift//swift:swift.bzl", "swift_library")
load("@cgrindel_bazel_starlib//bzltidy:defs.bzl", "tidy")
load("@rules_swift_package_manager//swiftpkg:defs.bzl", "swift_update_packages")
load(
    "@rules_xcodeproj//xcodeproj:defs.bzl",
    "top_level_target",
    "xcodeproj",
)

tidy(
    name = "tidy",
    targets = [
        ":swift_update_pkgs",
        ":update_build_files",
    ],
)

# MARK: - Gazelle

# Ignore the Swift build folder
# gazelle:exclude .build

gazelle_binary(
    name = "gazelle_bin",
    languages = [
        "@bazel_skylib_gazelle_plugin//bzl",
        "@rules_swift_package_manager//gazelle",
    ],
)

gazelle(
    name = "update_build_files",
    gazelle = ":gazelle_bin",
)

swift_update_packages(
    name = "swift_update_pkgs",
    gazelle = ":gazelle_bin",
    generate_swift_deps_for_workspace = False,
    update_bzlmod_stanzas = True,
)

# gazelle:swift_default_module_name ModuleExample

xcodeproj(
    name = "xcodeproj",
    project_name = "App",
    tags = ["manual"],
    top_level_targets = [
        top_level_target(
            ":App",
            target_environments = ["simulator"],
        ),
        # ":Tests",
    ],
)

swift_library(
    name = "ModuleExample",
    srcs = [
        "AppDelegate.swift",
        "ListViewController.swift",
        "demos/BasicUIScrollViewController.swift",
    ],
    module_name = "ModuleExample",
    tags = ["manual"],
    visibility = ["//visibility:public"],
    deps = [
        "@swiftpkg_sentry_cocoa//:Sources_Sentry",
        "@swiftpkg_snapkit//:Sources_SnapKit",
    ],
)

ios_application(
    name = "App",
    app_icons = glob(["Assets.xcassets/AppIcon.appiconset/**"]),
    bundle_id = "io.app.Example-iOS",
    families = [
        "iphone",
    ],
    infoplists = [":Info.plist"],
    launch_storyboard = ":Base.lproj/LaunchScreen.storyboard",
    minimum_os_version = "11.0",
    resources = glob(
        ["Assets.xcassets/**"],
        exclude = ["Assets.xcassets/AppIcon.appiconset/**"],
    ),
    visibility = ["//visibility:public"],
    deps = [":ModuleExample"],
)

build_test(
    name = "build_test",
    targets = [
        ":App",
    ],
)
